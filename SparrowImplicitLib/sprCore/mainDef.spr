import basicDecls(Char, Int8, Int, StringRef, Null, typeOf, reinterpretCast, sizeOf, UntypedPtr, ptrAdd, ptrSub, ptrDiff, fromCString, *)

/// Range type that returns StringRef objects referring to program arguments
datatype MainParameters
    using RetType = StringRef

    _begin, _end: _Impl.CStrPtr

[protected]
    fun ctor(this: @MainParameters, argc: Int, argv: @ @Char)
        this._begin = (_Impl.fromArgvPtr(argv))
        this._end = (this._begin + argc)

    fun isEmpty(this: MainParameters)                      = this.size == 0
    fun size(this: MainParameters): Int               = _end - _begin

    fun front(this: MainParameters): RetType               = _begin toStringRef
    fun back(this: MainParameters): RetType                = _end - 1 toStringRef
    fun ()(this: MainParameters) : RetType                 = _begin toStringRef
    fun ()(this: MainParameters, n: Int): RetType     = _begin + n toStringRef

    fun popFront(this: @MainParameters)                    { _begin = (_begin + 1); }
    fun popBack(this: @MainParameters)                     { _end = (_end - 1); }
    fun popFront(this: @MainParameters, n: Int)       { _begin = (_begin + n); }
    fun popBack(this: @MainParameters, n: Int)        { _end = (_end - n); }

package _Impl
    [bitcopiable]
    datatype CStr { cstr: @Char; }
    [initCtor, bitcopiable]
    datatype CStrPtr { ptr: @CStr; }

    fun fromArgvPtr(argv: @ @Char): CStrPtr = CStrPtr(reinterpretCast(@CStr, argv))
    fun toCStrPtr(p: UntypedPtr): CStrPtr = CStrPtr(p.asRefOf(CStr))
    fun toUntypedPtr(p: CStrPtr) = UntypedPtr(p.ptr)
    fun +(p: CStrPtr, n: Int): CStrPtr = toUntypedPtr(p) ptrAdd (n*sizeOf(CStrPtr)) toCStrPtr
    fun -(p: CStrPtr, n: Int): CStrPtr = toUntypedPtr(p) ptrSub (n*sizeOf(CStrPtr)) toCStrPtr
    fun -(p, q: CStrPtr): Int = ptrDiff(toUntypedPtr(p), toUntypedPtr(q)) / (sizeOf(CStrPtr))

    fun toStringRef(p: CStrPtr): StringRef = fromCString(p.ptr.cstr)

var programArgs: MainParameters

// To be defined by the application:
fun sprMain

// The "main" function will be declared in 'mainImpl.spr'
