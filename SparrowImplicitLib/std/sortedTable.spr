module std.sortedTable

import vector(Vector)
import ranges(rangeSize)

class SortedTable(keyType, valueType, valueToKeyType, lessType, compareType: Type)
    using KeyType = keyType
    using ValueType = valueType
    using ValueToKeyType = valueToKeyType
    using LessType = lessType
    using CompareType = compareType
    using RangeType = Vector(valueType).RangeType

    fun ctor
        this.ctor(0, LessType(), CompareType())

    fun ctor(n: SizeType, less: LessType, comp: CompareType)
        this._table ctor
        this._valToKey ctor
        this._less ctor less
        this._comp ctor comp
        if n > 0
            this.reserve(n)

    fun ctor(range: AnyType, n: SizeType, less: LessType, comp: CompareType)
        this._table ctor
        if n > 0
            _table.reserve(n)
        else
            _table.reserve(rangeSize(range))
        this._valToKey ctor
        this._less ctor less
        this._comp ctor comp

        //_table.assign(range)
        //sort(_table.all, less)
        for v = range
            this.insert(v)

    fun ctor(other: @SortedTable)
        _table ctor other._table
        _less ctor other._less
        _comp ctor other._comp

    fun =(other: @SortedTable): @SortedTable
        _table = other._table
        _valToKey = other._valToKey
        _comp = other._comp
        _less = other._less
        _valToKey = other._valToKey

        return this

    fun ==(other: @SortedTable): Bool
        return _valToKey == other._valToKey && _less == other._less && _comp == other._comp && _table == other._table

    fun !=(other: @SortedTable): Bool = !(this == other)

    var _table: Vector(ValueType)
    var _valToKey: ValueToKeyType
    var _less: LessType
    var _comp: CompareType

fun swap(this: @SortedTable, other: typeOf(this))
    _table.swap(other._table)

    var tmpv = _valToKey
    _valToKey = other._valToKey
    other._valToKey = tmpv

    var tmpl = _less
    _less = other._less
    other._less = tmpl

    var tmpc = _comp
    _comp = other._comp
    other._comp = tmpc

fun lessFunction(this: @SortedTable)    = _less
fun comparator(this: @SortedTable)      = _comp

fun isEmpty(this: @SortedTable)         = _table.isEmpty
fun size(this: @SortedTable)            = _table.size
fun capacity(this: @SortedTable)        = _table.capacity

fun all(this: @SortedTable)             = _table.all

fun reserve(this: @SortedTable, n: SizeType)
    _table.reserve(n)

fun insert(this: @SortedTable, value: @this.ValueType): RangeType
    var r = this.lowerBound(_valToKey(value))
    var idx = _table.size - r.size
    if r.isEmpty || !_comp(_valToKey(value), _valToKey(r.front))
        _table.insertBefore(value, r)
    var res = _table.all
    res.popFront(idx)
    return res

fun insert(this: @SortedTable, range: Range)
    _table.reserve(_table.size + rangeSize(range))
    for v = range
        var r = this.lowerBound(_valToKey(v))
        if r.isEmpty || !_comp(_valToKey(v), _valToKey(r.front))
            _table.insertBefore(v, r)

fun remove(this: @SortedTable, key: @this.KeyType)
    if this.isEmpty
        return

    var r = this.find(key)
    if !r.isEmpty
        var idx = r.frontPtr().diff(_table.all().frontPtr)
        _table.remove(idx)

fun remove(this: @SortedTable, range: this.RangeType)
    if !range.isEmpty
        _table.remove(range)

fun contains(this: @SortedTable, key: @this.KeyType): Bool  = !this.find(key).isEmpty
fun count(this: @SortedTable, key: @this.KeyType): SizeType = ife(this.find(key).isEmpty, 0, 1)

fun lowerBound(this: @SortedTable, key: @this.KeyType): RangeType
    var first: SizeType = 0
    var len = _table.size
    while len > 0
        var l2 = len / 2
        var el: @ValueType = _table(first + l2)
        if _less(_valToKey(el), key)
            first += l2+1
            len -= l2+1
        else
            len = l2
    var r = _table.all
    r.popFront(first)
    return r

fun upperBound(this: @SortedTable, key: @this.KeyType): RangeType
    var first: SizeType = 0
    var len = _table.size
    while len > 0
        var l2 = len / 2
        var el: @ValueType = _table(first + l2)
        if _less(key, _valToKey(el))
            len = l2
        else
            first += l2+1
            len -= l2+1
    var r = _table.all
    r.popFront(first)
    return r

fun equalRange(this: @SortedTable, key: @this.KeyType): RangeType
    var r = this.find(key)
    if !r.isEmpty
        return RangeType(r.frontPtr, r.frontPtr().advance(1))
    else
        return RangeType()

fun find(this: @SortedTable, key: @this.KeyType): RangeType
    var r = this.lowerBound(key)
    if !r.isEmpty && _comp(key, _valToKey(r.front))
        return r
    else
        return RangeType()

fun clear(this: @SortedTable)
    _table.clear

[rtct] class Less(type: Type) if isValid(#$type < #$type)
    fun ()(l, r: @type) = l < r

