module std.string

import contiguousMemoryRange(ContiguousMemoryRange)
import ranges(rangeSize)
import rawPtr(RawPtr, allocRawPtr)

// TODO: Make sure that we have the same performance for Strings that we would have for basic char arrays
// TODO: Can we implement this in terms of Vector?
// TODO: Can we implement a VectorDetail(Type, InnerSpace), and implement fun Vector(type) = VectorBase(type, 0)

/// String class that has ownership of the characters
[convert]
datatype String
    using ValueType = Char
    using ValuePassType = Char
    using RangeType = StringRef

    using _initialCapacity = SizeType(16)
    using _growthFactor = 1.5
    using _PtrType = RawPtr(Char)

    _begin, _end, _endOfStore: _PtrType

fun ctor(this: !String)
    _begin ctor
    _end ctor
    _endOfStore ctor
fun ctor(this: !String, size: SizeType)
    _begin       ctor allocRawPtr(Char, size+1)
    _end         ctor _begin.advance(DiffType(size))
    _endOfStore  ctor _end
    _end.value = Char(0)
fun ctor(this: !String, count: SizeType, ch: Char)
    this.ctor(count)
    memset(_begin.bytePtr, reinterpretCast(@Byte, ch), count)
fun ctor(this: !String, other: String const)
    let size = other size
    this.ctor(size)
    memcpy(_begin.bytePtr, other._begin.bytePtr, size)
[convert]
fun ctor(this: !String, other: StringRef)
    let size = other size
    this.ctor(size)
    memcpy(_begin.bytePtr, other.begin, size)
fun ctor(this: !String, _begin, _end: RawPtr(Char))
    let size = _end.diff(_begin)
    this.ctor(size)
    memcpy(this._begin.bytePtr, _begin.bytePtr, size)
fun ctor(this: !String, range: Range) if typeOf(range) != String
    this.ctor(rangeSize(range))
    var i=0
    for ch = range
        _begin.advance(DiffType(i++)).value = ch

[protected]
    fun ctorFromCt(this: !String, src: String ct)
        this ctor src.asStringRef

    fun dtor(this: !String)
        _begin.freePtr

    fun =(this: !String, other: String const): @String
        let tmp = other
        tmp.swap(this)
        return this

    fun ==(this, other: String const): Bool
        if this.size != other.size
            return false

        var i: SizeType = 0
        let s = this.size

        while i < s ; i++
            if this(i) != other(i)
                return false
        return true

    fun !=(this, other: String const): Bool = !(this == other)

    fun swap(this, other: !String)
        this._begin      swap    other._begin
        this._end        swap    other._end
        this._endOfStore swap    other._endOfStore

    fun asStringRef(this: String const)             = StringRef(_begin.bytePtr, _end.bytePtr)

    fun size(this: String const): SizeType          = _end.diff(_begin)
    fun isEmpty(this: String const)                 = _begin == _end
    fun capacity(this: String const): SizeType      = _endOfStore.diff(_begin)

    fun at(this: String const, index: SizeType)     = _begin.advance(DiffType(index)).value
    fun ()(this: String const, index: SizeType)     = _begin.advance(DiffType(index)).value
    fun front(this: String const)                   = _begin.value
    fun back(this: String const)                    = _end.advance(-1).value
    fun all(this: String const)                     = StringRef(_begin.bytePtr, _end.bytePtr)
    fun subrange(this: String const, index: SizeType, num: SizeType): StringRef \
                                = StringRef(_begin.advance(DiffType(index)).bytePtr, _begin.advance(DiffType(index + num)).bytePtr)

    fun pushBack(this: !String, value: Char)
        if _end == _endOfStore
            var t = 2 * this.capacity

            if t < 2
                t = 2
            this.reserve(t)
        _end.value ctor value
        _end = _end.advance
    fun +=(this: !String, value: Char)
        this.pushBack(value)

    fun popBack(this: !String)
        _end = _end advance -1
        _end.value dtor

    fun resize(this: !String, n: SizeType)
        let oldSize = this.size

        if n == oldSize
            return

        if n < oldSize
            let newEnd = _begin.advance(DiffType(n))

            while _end != newEnd
                _end = _end.advance(-1)
                _end.value dtor
        else
            this.reserve(n)

            let newEnd = _begin.advance(DiffType(n))

            while _end != newEnd
                _end.value ctor
                _end = _end.advance

    fun reserve(this: !String, n: SizeType)
        let curCapacity = this.capacity

        if n <= curCapacity
            return

        if n < _initialCapacity
            n = _initialCapacity
        if n <_growthFactor*curCapacity
            n = SizeType(_growthFactor*curCapacity)

        let curSize = this.size

        _begin.reallocPtr(n)
        _end = _begin.advance(DiffType(curSize))
        _endOfStore = _begin.advance(DiffType(n))

    fun clear(this: !String)
        this.remove(StringRef(_begin.bytePtr, _end.bytePtr))

    fun insertBefore(this: !String, value: Char, pos: StringRef)
        let posCount = _frontPtr(pos).diff(_begin)

        this.reserve(this.size + 1)

        var p = _end.advance(-1)
        var q = _begin.advance(posCount - DiffType(1))

        while p != q ; p = p.advance(-1)
            p.advance().value ctor p.value
            p.value dtor
        p.advance().value ctor value
        _end = _end.advance

    fun insertBefore(this: !String, range: Range, pos: StringRef)
        let n = range.size
        let index = _frontPtr(pos).diff(_begin)

        this.reserve(this.size + n)

        var p = _end.advance(-1)
        let q = _begin.advance(index - DiffType(1))

        while p != q ; p = p.advance(-1)
            p.advance(DiffType(n)).value ctor p.value
            p.value dtor
        p = p.advance
        while !range.isEmpty
            p.value ctor range.front
            p = p.advance
            range.popFront
        _end = _end.advance(DiffType(n))

    fun insertAfter(this: !String, value: Char, pos: StringRef)
        this.insertBefore(value, StringRef(_backPtr(pos).bytePtr, _end.bytePtr))

    fun insertAfter(this: !String, range: Range, pos: StringRef)
        this.insertBefore(range, StringRef(_backPtr(pos).bytePtr, _end.bytePtr))

    fun append(this: !String, range: Range)
        this.insertBefore(range, StringRef(_end.bytePtr, _end.bytePtr))
    fun append(this: !String, value: Char)
        this.insertBefore(value, StringRef(_end.bytePtr, _end.bytePtr))

    fun remove(this: !String, range: StringRef)
        let rSize = range.size
        var rBegin = _frontPtr(range)
        let rEnd = rBegin.advance(DiffType(rSize))

        while rEnd != _end
            rBegin.value dtor
            rBegin.value ctor rEnd.value
            rBegin = rBegin.advance
            rEnd = rEnd.advance
        _end = rBegin

    fun remove(this: !String, index: SizeType)
        var r = this.all

        r.popFront(index)
        r.popBack(this.size - index - 1)
        this.remove(r)

    fun assign(this: !String, range: Range)
        this.clear
        for v = range
            this.pushBack(v)

    fun >>(this: String const, os: !OutStream)
        os << this.asStringRef

    fun _frontPtr(s: StringRef) = RawPtr(Char)(s.begin)
    fun _backPtr(s: StringRef) = RawPtr(Char)(s.end)

// TODO
[autoCt]
fun + (x,y: String): String
    let sz1 = x.size
    let sz2 = y.size
    var res: String = sz1 + sz2
    memcpy(res._begin.bytePtr, x._begin.bytePtr, sz1)
    memcpy(res._begin.advance(sz1).bytePtr, y._begin.bytePtr, sz2)
    return res

[autoCt]
fun + (x: String, y: StringRef): String
    let sz1 = x.size
    let sz2 = y.size
    var res: String = sz1 + sz2
    memcpy(res._begin.bytePtr, x._begin.bytePtr, sz1)
    memcpy(res._begin.advance(sz1).bytePtr, y.begin, sz2)
    return res

//[autoCt]
//fun + (x: String, y: Char): String
//    let sz1 = x.size
//    let sz2 = sizeOf(y)
//    var res: String = sz1 + sz2
//    memcpy(res._begin, x._begin, sz1)
//    memcpy(ptrAdd(res._begin, sz1), reinterpretCast(@Byte, y), sz2)

//    return res

fun intToString(x: Int): String
    var buf: static_buffer(12)
    _intToCString(x, reinterpretCast(@Char, buf))
    return String(fromCString(reinterpretCast(@Char, buf)))
fun ulongToString(x: ULong): String
    var buf: static_buffer(16)
    _ulongToCString(x, reinterpretCast(@Char, buf))
    return String(fromCString(reinterpretCast(@Char, buf)))

// TODO

datatype StringOutputStream
    result: String

[protected]
    fun <<<(this: !StringOutputStream, s: String const)  { result.append(s.all) }
    fun <<<(this: !StringOutputStream, s: StringRef)     { result.append(s) }
    fun <<<(this: !StringOutputStream, x: Char)          { result.append(x) }
    fun <<<(this: !StringOutputStream, x: Int)           { result.append(intToString(x).all) }
    fun <<<(this: !StringOutputStream, x: ULong)         { result.append(ulongToString(x).all) }
    fun <<<(this: !StringOutputStream, x: Double)        { result.append(ulongToString(ULong(x)).all) } // Not properly supported

fun toString = String()
fun toString(a1: AnyType): String
    var s: StringOutputStream
    s << a1
    return s.result
fun toString(a1, a2: AnyType): String
    var s: StringOutputStream
    s << a1 << a2
    return s.result
fun toString(a1, a2, a3: AnyType): String
    var s: StringOutputStream
    s << a1 << a2 << a3
    return s.result
fun toString(a1, a2, a3, a4: AnyType): String
    var s: StringOutputStream
    s << a1 << a2 << a3 << a4
    return s.result
fun toString(a1, a2, a3, a4, a5: AnyType): String
    var s: StringOutputStream
    s << a1 << a2 << a3 << a4 << a5
    return s.result
fun toString(a1, a2, a3, a4, a5, a6: AnyType): String
    var s: StringOutputStream
    s << a1 << a2 << a3 << a4 << a5 << a6
    return s.result
fun toString(a1, a2, a3, a4, a5, a6, a7: AnyType): String
    var s: StringOutputStream
    s << a1 << a2 << a3 << a4 << a5 << a6 << a7
    return s.result

[native("_Int_to_CString")] fun _intToCString(x: Int, buffer: @Char)
[native("_ULong_to_CString")] fun _ulongToCString(x: ULong, buffer: @Char)
