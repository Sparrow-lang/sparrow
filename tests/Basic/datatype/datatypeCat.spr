//!! -t "../SparrowImplicitLib.spr" -fno-main

// Test purpose: datatypes with cat-type fields

[bitcopiable, initCtor]
datatype Pos
    x: SizeType
    y: SizeType

fun dtor(this: !Pos)
    write('dtor ')
    write(x)
    write(' ')
    writeLn(y)

fun mutableOp(this: !Pos)
    write('mutableOp ')
    write(x)
    write(' ')
    writeLn(y)

datatype MyData
    a: Pos
    b: !Pos
    c: Pos const
    d: Pos tmp

fun ctor(this: !MyData)
    a.ctor(1,1)
    b.ctor(2,2)
    c.ctor(3,3)
    d.ctor(4,4)

fun dtor(this: !MyData)
    d.dtor
    c.dtor
    b.dtor
    a.dtor

fun print(this: MyData)
    write(a.x); write(' '); writeLn(a.y)
    write(b.x); write(' '); writeLn(b.y)
    write(c.x); write(' '); writeLn(c.y)
    write(d.x); write(' '); writeLn(d.y)

fun changeWhileConst(this: MyData)
    //a = Pos(5, 5) // should fail
    b = Pos(5, 5) // ok, b is mutable
    //c = Pos(5, 5) // should fail
    //d = Pos(5, 5) // should fail

    //a.x = 10 // should fail
    b.x = 10 // ok, b is mutable
    //c.x = 10 // should fail
    //d.x = 10 // should fail
    

[native("test")] fun test(n: Int)
    var o1: MyData
    writeLn(sizeOf(SizeType))
    writeLn(sizeOf(Pos))
    writeLn(sizeOf(o1))
    writeLn('---')
    o1 changeWhileConst
    writeLn('---')
    o1.a mutableOp
    o1.b mutableOp
    //o1.c mutableOp
    o1.d mutableOp
    writeLn('---')
    o1 print

/*<<<Running()
8
16
64
---
dtor 5 5
---
mutableOp 1 1
mutableOp 10 5
mutableOp 4 4
---
1 1
10 5
3 3
4 4
dtor 4 4
dtor 3 3
dtor 10 5
dtor 1 1
>>>*/