//!!

concept Swappable(x) if isValid(x.swap(x))

[autoCt] fun swap(a, b: @AnyType) if typeOf(a) == typeOf(b) && !Swappable(a)
    cout << "Simple swap called" << endl
    let tmp = a
    a = b
    b = tmp
[autoCt] fun swap(a, b: @Swappable) if typeOf(a) == typeOf(b)
    cout << "Fast swap called" << endl
    a.swap(b)

[initCtor] datatype MyType1
    val: Int

package Inner
    [initCtor] datatype MyType2
        val: Int

    fun swap(this, other: @MyType2)
        let tmp = val
        val = other.val
        other.val = tmp

using Inner.MyType2

fun sprMain
    ;
        var x = 3
        var y = 5
        cout << x << ", " << y << endl
        swap(x, y)
        cout << x << ", " << y << endl
    cout << "---" << endl
        var x = MyType1(13)
        var y = MyType1(15)
        cout << x.val << ", " << y.val << endl
        swap(x, y)
        cout << x.val << ", " << y.val << endl
    cout << "---" << endl
        var x = MyType2(23)
        var y = MyType2(25)
        cout << x.val << ", " << y.val << endl
        swap(x, y)
        cout << x.val << ", " << y.val << endl
    ;

/*<<<Running()
3, 5
Simple swap called
5, 3
---
13, 15
Simple swap called
15, 13
---
23, 25
Fast swap called
25, 23
>>>*/
