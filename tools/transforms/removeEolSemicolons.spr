//! Transformation that removes semicolons at the end of the line
module formatTool.transforms.removeEolSemicolons

import utils
import formatDetails.sourceData

//! Remove end-of-line semicolons that we don't need
fun removeEolSemicolons(src: !SourceData)
    src _removePass1
    src _removePass2

//! Remove all the semicolons at the end of line, if:
//!     - indent of prev line is the same as cur line
fun _removePass1(src: !SourceData)
    var layout: LayoutHelper
    for i = 0..Int(src.tokens.size)
        var tok: @TokenData = src.tokens(i)
        var remaining = src.tokens.subrange(i+1)

        layout onToken tok

        var canDelete = tok.canFormat && tok.type == tkSEMICOLON && nextIsNewline(remaining)

        canDelete = canDelete && layout.prevLineIndent == layout.curLineIndent

        if canDelete
            clearTokenLine(i, src.tokens)

//! Remove all the semicolons at the end of line, if:
//!     - indent of next line is the same as cur line
//! This pass is used to clear any semicolons that are followed by a non-semicolon line of the same
//! indent
fun _removePass2(src: !SourceData)
    var layout: LayoutHelper
    for i = 0..Int(src.tokens.size)
        var tok: @TokenData = src.tokens(i)
        var remaining = src.tokens.subrange(i+1)

        layout onToken tok

        var canDelete = tok.canFormat && tok.type == tkSEMICOLON && nextIsNewline(remaining)

        canDelete = canDelete && layout.nextLineIndent(remaining) == layout.curLineIndent

        if canDelete
            clearTokenLine(i, src.tokens)
