module formatTool.transforms.removeExtraSpaces

import utils
import formatDetails.sourceData

fun removeExtraSpaces(src: @SourceData)
    _removeEndSpaces(src)
    _removeMultipleEmptyLines(src)

//! Remove spaces at the end of the line
fun _removeEndSpaces(src: @SourceData)
    for i = 0..Int(src.tokens.size)
        var tok: @TokenData = src.tokens(i)
        var remaining = src.tokens.subrange(i+1)
        if tok.canFormat && tok.type == tkWHITESPACE && tok.content.size > 0 \
            && nextIsNewline(remaining, false)
            tok.content = ''

//! Remove multiple consecutive empty lines
fun _removeMultipleEmptyLines(src: @SourceData)
    var lastLineWithVisible = 0
    var numNewlines = 0
    for i = 0..Int(src.tokens.size)
        var tok: @TokenData = src.tokens(i)
        var remaining = src.tokens.subrange(i+1)

        if tok.type != tkWHITESPACE && tok.type != tkEOL
            lastLineWithVisible = Int(tok.loc.start.line)
            numNewlines = 0
        if tok.type == tkEOL
            if lastLineWithVisible != Int(tok.loc.start.line)
                numNewlines++

            if numNewlines >= 3 && tok.canFormat
                clearToken(tok)
